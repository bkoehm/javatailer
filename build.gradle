// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'software.javatailer'
version = '0.1.1'

buildscript {
   repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenLocal()
    // If using a Maven proxy, put the property settings in
    // ~/.gradle/gradle.properties for default_proxy_url,
    // default_proxy_username and default_proxy_password.
    if (hasProperty("default_proxy_url")) {
        maven {
            url default_proxy_url
            credentials {
                username property("default_proxy_username")
                password property("default_proxy_password")
            }
        }
    }
    jcenter()
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // Slf4j and Log4j2
    compile 'org.slf4j:slf4j-api:1.7.20'
    def log4jVersion = '2.5'
    testCompile "org.apache.logging.log4j:log4j-core:$log4jVersion"
    testCompile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"

    testCompile 'junit:junit:4.12'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

if(hasProperty('default_bintray_user')) {
    bintray {
        // put in ~/.gradle/gradle.properties
        user =  property('default_bintray_user')
        key =  property('default_bintray_api_key')
        publications = ['mavenJava']

        //dryRun = true

        pkg {
            repo = 'maven'
            name = 'javatailer'
            desc = 'JavaTailer - tail for Java'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/bkoehm/javatailer.git'
            issueTrackerUrl = 'https://github.com/bkoehm/javatailer/issues'
            version {
                name = version
                released = new Date()
            }
        }
    }
}
